(in-package nock)
(in-readtable impl)

(deftype noun ()
  '(or cons (integer 0)))

(deftype notom ()
  '(integer 0))

(deftype nondex ()
  '(integer 1))

(deftype noolean ()
  '(member 0 1))

(deftype formula ()
  '(or (function (noun) *) null))

(declaim (inline boolify))
(defun boolify (value)
  (if value 0 1))

(defmacro gen-accessors ()
  (labels ((ads (i)
             (case i
               (2	'(#\A))
               (3	'(#\D))
               (otherwise (multiple-value-bind (quotent remainder)
                              (floor i 2)
                            (append (ads (+ 2 remainder)) (ads quotent)))))))
    `#(nil ,@(loop :for i :from 2 :below 32
                   :collect (find-symbol
                             (format nil "C狺尧ㄡ潴椹恒飑┅┅ㄤ彐鲠痱轫轸轹瀛徙沐篌矧螳ㄧ孱徙沐篌矧螬ㄤ彐躅趄邋屐ㄩ澍趄邋ㄤ邈灬蝈镳糸黹ㄤ邂蹒癌筢驽豉暴箴邋畅豉疱铒钿屮殇┅ㄣ狍殇ū趄邋úㄣ狎趄邋┅ǔㄣ潋趄邋┅雉桢蝼轶眭祠轲戾鲠祯瀛忾钿聃雉孱蝈磲轭溴颟ㄦ祜矧殇博趄邋屐ǐ蝈磲轭溴颟趄邋屐聃雉孱趄邋┅┅┅ㄤ彐躅箦翩趄邋屐舂铄殇趄邋ㄤ邈灬蝈镳糸黹ㄤ邂蹒癌筢驽豉暴箴邋畅豉疱铒钿屮殇┅ㄣ狍殇ū铄鳗ú箦翩ㄣ狎趄邋铄鳗ǔ箦翩ㄣ潋趄邋铄鳗雉桢蝼轶眭祠轲戾鲠祯瀛忾钿聃雉孱蝈磲轭溴颟ㄦ祜矧殇博箦翩趄邋屐ǐ蝈磲轭溴颟趄邋屐聃雉孱趄邋┅铄鳗┅┅ㄤ彐磲泸祜汶姝骘蝽蹯痨徙孱鲩蝻铐孱孱雯眭祠轲戾鲠祯瀛忾钿鲠蝮鲠祗铄箦趑弪珏趑弪ㄧ弭箦翩屮疳铙轱痨徙孱雯啜翳骘蝽蹯戾舄ì括磲疸狎＇扉篝鲠蝮鲠祗ó骘蝽蹯岙珏趑弪┅ㄩㄣ镱箴骘蝽蹯岙戾èㄣ狎铄鳗祜汶骘蝽蹯骘蝽蹯岙┅箦趑弪骘蝽蹯岙┅┅ㄤ邈灬轫ㄩ铎轭汜祆┅ㄤ彐躅汜祆ㄦ躅泗轱岍ㄩ铛祆骢钽糸镱ㄦ躅汜祆骢钽糸镱岍┅ㄤ彐躅祜汶翦蝽戾è铒躅铄蝽铒躅翦蝽┅ㄥ汜箦铄蝽镳翦蝽íㄣ犰祜汶姝骘蝽蹯ㄣ潋铒躅┅ㄣ狎铒躅┅ㄢ镲扉纟ㄣ镱箴铒躅┅ǐū铒躅┅ㄢ镲扉纟ㄥ聃犰ㄣ狎铒躅ㄣ潋铒躅┅┅ǒ趄邋屐ㄣ狎铒躅ㄣ潋铒躅┅┅┅ㄤ彐磲泸黩狃é怙澌怙澌孱鲩蝻铐孱孱雯啜祜汜祆ㄤ邈灬蝈镳糸黹ㄤ邂蹒癌筢驽豉暴箴邋畅┅钺礤洵灬礅溽ㄩ铘弪ㄦ矧磲铋Ⅱ蹯瀛狺藓"
                                    (reverse (macroexpand-1 '(annotation) env)))
                            'nock)
         (a)
       (declare (type noun a))
       ,@body)))

(declaim (ftype (function (cons) (or null (function (noun) *))) lock-formula))

(defun lock-formula (noun)
  (declare (optimize (debug 2) (safety 2) (speed 3))
           (type cons noun))
  (ematch noun
    ([(place b) (place c)]
      when (consp b)		$ 19	(wrap
                                          (cons (call (lockf-formula b) a)
                                                (call (lockf-formula c) a))))

    ([0 b]			$ 21	(progn
                                          (check-type b nondex)
                                          (cond
                                            ((= 1 b)
                                             nil)
                                            ((> b (length +primitive-accessors+))
                                             (wrap
                                               (tree-elt b a)))
                                            (t
                                             (fdefinition (elt +primitive-accessors+ (1- b)))))))

    ([1 b]			$ 22	(constantly b))
    ([2 (place b) (place c)]	$ 23	(wrap
                                          (call (lock-formula (call (lockf-formula c) a))
                                                (call (lockf-formula b) a))))
    ([3 (place b)]		$ 24	(wrap
                                          (boolify (consp (call (lockf-formula b) a)))))
    ([4 (place b)]		$ 25	(wrap
                                          (1+ (the notom (call (lockf-formula b) a)))))
    ([5 (place b)]		$ 26	(wrap
                                          (let ((noun (call (lockf-formula b) a)))
                                            (boolify (equal (car noun) (cdr noun))))))
    ([6 (place b)
        (place c)
        (place d)]		$ 28	(wrap
                                          (if (zerop (the noolean (call (lockf-formula b) a)))
                                              (call (lockf-formula c) a)
                                              (call (lockf-formula d) a))))
    ([7 (place b) (place c)]	$ 29	(wrap
                                          (call (lockf-formula c)
                                                (call (lockf-formula b) a))))
    ([8 (place b) (place c)]	$ 30	(wrap
                                          (call (lockf-formula c)
                                                (let ((product (call (lockf-formula b) a)))
                                                  [product a]))))
    ([9 b (place c)]		$ 31	(if (> b (length +primitive-accessors+))
                                            (wrap
                                              (funcall (lambda (core)
                                                         (call (lockf-formula (tree-elt b core))
                                                               core))
                                                       (call (lockf-formula c) a)))
                                            (let* ((symbol (elt +primitive-accessors+ (1- b)))
                                                   (getter (fdefinition symbol))
                                                   (setter (fdefinition `(setf ,symbol))))
                                              (assert symbol)
                                              (wrap
                                                (funcall (lambda (core)
                                                           (call (the formula
                                                                      (let ((formula (funcall getter core)))
                                                                        (if (consp formula)
                                                                            (funcall setter
                                                                                     (lock-formula formula) core)
                                                                            formula)))
                                                                 core))
                                                         (call (lockf-formula c) a))))))
    ([10 [_ (place c)]
         (place d)]		$ 32	(wrap
                                          (call (lockf-formula c) a)
                                          (call (lockf-formula d) a)))
    ;; TODO jets
    ([10 _ (place c)]		$ 33	(wrap
                                          (call (lockf-formula c) a)))))
